snekQL {
  Program      = Statement*
  Statement    = Dec+
               | "if" Exp1 ":" Suite
                 ("elif" Exp1 ":" Suite)*
                 ("else" ":" Suite)?                    -- if
               | "while" Exp1 ":" Suite                 -- while
               | "for" id "in" Statement ":" Suite      -- for
               | Lvalue "=" Exp1                        -- assign
               | "break"                                -- break
               | Print
               | Exp1

  Suite        = indent Statement* dedent

  Dec          = VarDec | FunDec
  FunDec       = "fnc" id Params ":" Statement
  VarDec       = id assignop Exp1
  Params       = "(" ListOf<Param, ","> ")"
  Param        = id ":" Type


  Exp1         = Exp1 "or" Exp2                              -- binary
               | Exp2
  Exp2         = Exp2 "and" Exp3                              -- binary
               | Exp3
  Exp3         = Exp4 relop Exp4                            -- binary
               | Exp4
  Exp4         = Exp4 addop Exp5                            -- binary
               | Exp5
  Exp5         = Exp5 mulop Exp6                            -- binary
               | Exp6
  Exp6         = Exp6 "**" Exp7                             --binary
               | Exp7
  Exp7         = "-" Exp8                                   --negation
               | Exp8
  Exp8         = Call | Literal | Lvalue | Rule | Arr

  Literal      = numlit
               | strlit
               | boolit
               | "null"                                    -- null

  Lvalue       = Lvalue "[" Exp1 "]"                         -- subscripted
               | Lvalue "." (Call | id)                           -- field
               | id                                         -- id
  Binding      = id "=" Exp1
  Call         = id "(" ListOf<Exp1, ","> ")"
  Return       = "return" Exp1         -- returnExpression
               | "return"             -- returnNothing

  Type         = "arr" | "boo" | "dub" | "int" | "str"

  keyword      = ("break" | "else" |"elif" | "false" | "for" | "fnc" | "if" | "in"
               | "null" | "hiss" | "return" | "true" | "while") ~idrest
  id           = ~keyword letter idrest*
  idrest       = letter | digit | "_"

  indent      =  "⇨"
  dedent      =  "⇦"

  Rule         = "@" relop Exp1+
  Arr          =  "[" ListOf<Exp1, ","> "]"
  Print        = "hiss(" (strlit | Exp1) ")"
  numlit       =  digit+ ("." digit+)?
  strlit       = "\"" (~"\""any)* "\""
  boolit       = "true" | "false"

  escape       = "\\" ("\\" | "\"" | "n" | "t")
  assignop     = "=" | "*=" | "/=" | "+=" | "-="
  mulop        = "*" | "%" | "//" |"/"
  addop        = "+" | "-"
  relop        = "<=" | "!=" | "<" | ">=" | ">" | "=="
  space       := " " | "\t" | "\n" | comment
  comment      = "#" (~"\n" any)*                   --singlelineComment
               | "~~~" (~"~~~:" any)* "~~~:"        --multilineComment

}
